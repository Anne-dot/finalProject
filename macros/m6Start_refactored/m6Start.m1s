' M6 Tool Change Macro


' CONFIGURATION CONSTANTS

' Tool Spacing Configuration
Const toolSpaceX = 32          ' Distance between tools X axis (mm)
Const toolSpaceY = 32          ' Distance between tools Y axis (mm)


' Magazine Offset Positions

Const M1_OFFSET_X = 81         ' Magazine 1 X offset (77+4)
Const M2_OFFSET_X = -60.9      ' Magazine 2 X offset (-66+5.1)
Const M1M2_OFFSET_Y = 206      ' Common Y offset for M1 and M2
Const M3_OFFSET_X = -208       ' Magazine 3 X offset
Const M3_OFFSET_Y = 263        ' Magazine 3 Y offset


' Output Signal Mapping

Const bits0 = OUTPUT5          ' Tool selector bit 0
Const bits1 = OUTPUT6          ' Tool selector bit 1
Const bits2 = OUTPUT7          ' Tool selector bit 2
Const bits3 = OUTPUT8          ' Tool selector bit 3
Const m1Out = OUTPUT2          ' Magazine 1 selector
Const m2Out = OUTPUT3          ' Magazine 2 selector
Const m3Out = OUTPUT4          ' Magazine 3 selector


' SIGNAL CONTROL FUNCTIONS

Sub DeactivateAllToolSignals()

    DeactivateSignal(bits0)
    DeactivateSignal(bits1)
    DeactivateSignal(bits2)
    DeactivateSignal(bits3)
    DeactivateSignal(m1Out)
    DeactivateSignal(m2Out)
    DeactivateSignal(m3Out)
    sleep(1000)

End Sub

Sub ActivateToolSignals(muxOut, mSel)

    If ((muxOut And 1) = 1) Then
        ActivateSignal(bits0)
    End If
    If ((muxOut And 2) = 2) Then
        ActivateSignal(bits1)
    End If
    If ((muxOut And 4) = 4) Then
        ActivateSignal(bits2)
    End If
    If ((muxOut And 8) = 8) Then
        ActivateSignal(bits3)
    End If
    If (mSel = 1) Then
        ActivateSignal(m1Out)
    End If
    If (mSel = 2) Then
        ActivateSignal(m2Out)
    End If
    If (mSel = 3) Then
        ActivateSignal(m3Out)
    End If

End Sub


' MAIN TOOL CHANGE ROUTINE

Sub M6Start()

    Dim tool, toolData

    DeactivateAllToolSignals()
 
    tool = GetSelectedTool()
    Dim xOff, yOff, muxOut, mSel
    xOff = 0
    yOff = 0
    muxOut = 0
    mSel = 0

    Select Case tool 
        Case 0
            xOff = 0
            yOff = 0
            muxOut = 0
            mSel = 0
        Case 21
            xOff = M3_OFFSET_X
            yOff = M3_OFFSET_Y
            muxOut = 0
            mSel = 3
        Case 22
            xOff = toolSpaceX * 3
            yOff = toolSpaceY * -2
            muxOut = 1
        Case 23
            xOff = toolSpaceX * 2
            yOff = toolSpaceY * -3
            muxOut = 2
        Case 24
            xOff = toolSpaceX *2
            yOff = toolSpaceY *-2
            muxOut = 3
        Case 25
            xOff = toolSpaceX *2
            yOff = toolSpaceY *-1	
            muxOut = 4
        Case 26
            xOff = toolSpaceX *2
            yOff = toolSpaceY *0	
            muxOut = 5
        Case 27
            xOff = toolSpaceX *2
            yOff = toolSpaceY *1	
            muxOut = 6
        Case 28
            xOff = toolSpaceX *-2
            yOff = toolSpaceY *-1	
            muxOut = 7
        Case 29
            ' center tool
            xOff = toolSpaceX *0
            yOff = toolSpaceY *0	
            muxOut = 8
        Case 30
            xOff = toolSpaceX *-1
            yOff = toolSpaceY *0	
            muxOut = 9
        Case 31
            xOff = toolSpaceX *-2
            yOff = toolSpaceY *0	
            muxOut = 10
        Case 32
            xOff = toolSpaceX *-3
            yOff = toolSpaceY *0	
            muxOut = 11
        Case 33
            xOff = toolSpaceX *-4
            yOff = toolSpaceY *0	
            muxOut = 12
        Case 34
            xOff = toolSpaceX *-5
            yOff = toolSpaceY *0	
            muxOut = 13
        Case 35
            xOff = toolSpaceX *-6
            yOff = toolSpaceY *0	
            muxOut = 14
        Case 36
            xOff = toolSpaceX *-7
            yOff = toolSpaceY *0	
            muxOut = 15
        Case 37
            xOff = toolSpaceX *-2
            yOff = toolSpaceY *-1	
            muxOut = 16
        Case 42
            xOff = toolSpaceX * 3
            yOff = toolSpaceY * -2
            muxOut = 1
        Case 48
            xOff = toolSpaceX *-2
            yOff = toolSpaceY *-1	
            muxOut = 7
        Case Else
            muxOut = 0
            If (tool >= 1 And tool <= 10) Then
                xOff = M1_OFFSET_X
                yOff = M1M2_OFFSET_Y
                mSel = 1
            Else
                If (tool >= 11 And tool <= 20) Then
                    xOff = M2_OFFSET_X
                    yOff = M1M2_OFFSET_Y
                    mSel = 2
                Else
                    Message "Unknown Tool"
                    code("M0")
                End If
            End If

    End Select

    code("G52 X" & xOff & "Y" & yOff)
    sleep(500)         
    
    While IsMoving()
        sleep(100)         
    Wend 
    
    ActivateToolSignals(muxOut, mSel)
    SetCurrentTool(tool)             
    sleep(500)
End Sub